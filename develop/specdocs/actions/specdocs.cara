// -*- coding: utf-8 -*-

/** specdocsアプリケーションのリソース＆アクション 
 */
module specdocs in cara;

/** ディレクトリ・リソース
 */
resource Dir("/" | "*/") {
  /** ディレクトリへのGETリクエスト */
  action dir-get("/GET") 
  ::
  {
   %CATY_APP  | $.name > app;
   { 
     "wikifiles" : [lsdir %PATH_INFO .wiki, lsdir %PATH_INFO .lit]|list:concat | each {pv name},
     "subdirs"  : lsdir --kind=dir %PATH_INFO | each {pv name},
     "path" : %PATH_INFO,
     "app" : %app,
     "title" : [%app, " ", %PATH_INFO] | text:concat
   } | print /list-files.html
  };
};


/** Wikiファイル・リソース
 */
resource Wiki("*.wiki|*.casm.lit|*.cara.lit") {
  filetype {
   "contentType" : "text/x-wiki",
   "isText" : true,
  };

  /** WikiファイルへのGETリクエスト */
  action wiki-get("/GET")
  ::
  {
    {
      "content": file:read %0 | text:creole, 
      "title"  : %0 | path:trunk 
    } | print /show-wiki.html
  };
};


/** SysSpecs 
 */

resource SysSpecsLit ("/SysSpecs/*.casm.lit") {

  /** LiterateファイルへのGETリクエスト */
  action get("/GET#exists-parent")
  ::
  {
    %0 | path:base > base;
    ["schemata@SysSpecs:/", %base] | text:concat > lit-file;
    {
      "content": file:read %lit-file | text:creole, 
      "title"  : %0 | path:trunk 
    } | print /show-wiki.html
  };

};

/** global
 */

resource GlobalLit ("/global/*.casm.lit") {

  /** LiterateファイルへのGETリクエスト */
  action get("/GET#exists-parent")
  ::
  {
    %0 | path:base > base;
    ["schemata@global:/", %base] | text:concat > lit-file;
    {
      "content": file:read %lit-file | text:creole, 
      "title"  : %0 | path:trunk 
    } | print /show-wiki.html
  };

};
