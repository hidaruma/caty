// -*- coding: utf-8 -*-
/** WSGI仕様に基づくWeb処理のエミュレーション */
module wsgi in casm;

/** エンティティボディを表現する入力ストリーム 
 * Pythonのfile-likeオブジェクトである。
 */
type InputStream = foreign;

/** 出力ストリーム 
 * Pythonのfile-likeオブジェクトである。
 */
type OutputStream = foreign;


/** WSGIアプリケーションコールバックへの入力データ */
type Environ = {

  /** HTTPメソッド */
  "REQUEST_METHOD": httpMethod,

  /** アプリケーションを識別する名前 */
  "SCRIPT_NAME" : string,

  /** リクエストパス */
  "PATH_INFO": string,

  /** クエリー文字列 */
  "QUERY_STRING": string,

  /** リクエストボディのメディア型 */
  "CONTENT_TYPE": mediaType?,

  /** リクエストボディの長さ */
  "CONTENT_LENGTH": string,

  /** サーバーのホスト名 */
  "SERVER_NAME": string,

  /** サーバーのポート番号、ただし文字列 */
  "SERVER_PORT": string,

  /** 使っているHTTPプロトコル */
  "SERVER_PROTOCOL": "HTTP/1.1",

  /** リクエストボディ（入力）を表現するfile-likeオブジェクト */
  "wsgi.input": InputStream,

  /** エラー出力のストリーム 
   * PEP333では必須のようだが、Catyではオプショナルとする。
   */
  "wsgi.errors": OutputStream?, 

  /** マルチプロセス処理をしているか */
  "wsgi.multiprocess": boolean,

  /** マルチスレッド処理をしているか */
  "wsgi.multithread": boolean,

  /** リクエストごとに毎回プロセス起動をするか */
  "wsgi.run_once": false,

  /** http または https */
  "wsgi.url_scheme": ("http" | "https"),

  /** WSGIのバージョン */
  "wsgi.version": [1, 0],

  * : univ?,
};

/** HTTPリクエストを記述するデータ構造 */
type Request = {

  /** リクエストパス */
  "path": string,

  /** リクエストパスがフルパスかどうか */
  @[default(false)]
  "fullpath": boolean?,

  /** クエリー文字列
   * 文字列値のオブジェクトでもよい。
   */
  @[default("")]
  "query": (string|{*:string?})?,

  /** HTTPメソッド */
  @[default("GET")]
  "method": httpMethod?,

  /** 動詞 */
  @[default("")]
  "verb": string?,

  /** リクエストボディのメディア型  */
  "contentType": mediaType?,

  /** リクエストボディをテキストとみなすべきかどうか */
  "isText": boolean?,

  /** リクエストボディがテキストであるときの文字符号化方式 */
  @[typical(["utf-8"])]
  "encoding": string?,

  /** リクエストボディ */
  @[default(null)]
  "body": (null | string | binary)?,

  /** 送信するクッキー文字列 */
  "cookie": string?,

  /** その他リクエストヘッダ */
  "header": {
    *: string?,
  }?,
};

/** Webリクエスト情報をwsgi:Environ型のデータに変換する。
 *
 * # fullpathオプションの意味は、requestコマンドと同じである。 fullpath=falseのときは、現在のCatyアプリケーションを起点とする相対パスとなる。
 * # HTTPメソッドがGET, HEAD, DELETEのときは、入力はnullしか許容しない。
 * # HTTPメソッドがGET, HEAD, DELETEのときは、content-typeは指定できない。
 * # HTTPメソッドがPUT, POSTのときは、入力はstring|binaryが必須。
 * # HTTPメソッドがPUT, POSTのとき、入力がstringで content-typeは指定されてないなら、text/plain とみなす。
 * # HTTPメソッドがPUT, POSTのとき、入力がbinaryで content-typeは指定されてないなら、application/octet-streamとみなす。
 */
command make-env 
  {
    @[default(false)]
    "fullpath": boolean?,

    @[default("GET")]
    "method": string?,

    @[default("")]
    "verb": string?,

    "content-type": string?,

    @[default("http")]
    "url-scheme": string?,

    "server-name": string?, // デフォルトは設定から取るべきか？

    "server-port": integer?, // ここでは、文字列である必要性はないだろう。
     // デフォルトは設定から取るべきか？

    @[default(false)]
    "multiprocess": boolean?, // デフォルトは設定から取るべきか？

    @[default(true)]
    "multithread": boolean?, // デフォルトは設定から取るべきか？

  }
  [string path, string? query]
:: (null|string|binary) -> Environ
    throws InvalidInput
    refers python:caty.core.std.command.wsgilib.MakeEnv
;


command req-to-env :: Request -> Environ
    throws InvalidInput
    refers python:caty.core.std.command.wsgilib.ReqToEnv
;


type Response = {
    "status": integer,
    "header": {
        *: string?,
    },
    "body": (string | binary),
};

/** WSGIアプリケーションを呼び出す。
 * 
 * # 入力のwsgi:Environ型データを第一引数にしてWSGIアプリケーションを呼ぶ。
 * # 第二引数のstart_resposeは自前で準備したものを渡す。
 * # --no-middle=trueのときは、ミドルウェアスタックをバイバスする。つまり、 Catyが提示したWSGIアプリケーションを呼び出す。
 * # start_responseに渡された情報と、WSGIアプリケーションの戻り値からwsgi:Response型データを組み立てて出力とする。
 */
command call-application
  {
    @[default(false)]
    "no-middle": boolean?,
  }
:: Environ -> Response
  refers python:caty.core.std.command.wsgilib.CallApplication
;

command process-env
:: Environ -> Environ
  refers python:caty.core.std.command.wsgilib.ProcessEnv
;

command lookup-and-exec :: Environ -> any
  throws [ApplicationNotFound, FileNotFound, ActionNotFound]
  refers python:caty.core.std.command.wsgilib.LookupAndExec
;


