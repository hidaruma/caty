/**
 * XJSON操作のコマンド群
 */
module xjson in casm;

type AnyObject = {*:any};

/**
 * 引数で指定されたファイルを JSON データとして読み込み、値を返す。
 * JSON でないデータの書き込まれたファイルに対しての動作は保証しない（通常はエラー）。
 * --with-docオプションが指定された場合、ドキュメンテーション文字列を埋め込んだ形式で読み取る。
 */
command read {@[default(false)]"with-doc": boolean?} [string] :: void -> any
    reads [pub, data, sysfiles]
    refers python:xjsonlib.ReadJson;

/**
 * 引数で指定されたファイルに JSON データを書き込む。
 * このコマンドで書き込んだデータはXJSONフォーマットとなる。
 * json:readで読み出し可能な形式にしたい場合、--encode-jsonオプションを付けること。
 */
command write {@[default(false)]"encode-json": boolean?} [string] :: any -> void
    updates [data, pub]
    refers python:xjsonlib.WriteJson;

/**
 * 入力値文字列をパースして、JSONオブジェクトを出力する。
 */
command parse :: string -> any
    reads env
    refers python:xjsonlib.Parse;

/**
 * XJSON->JSONの変換
 */
command to-json :: StandardXJson ->StandardJson
    refers python:xjsonlib.ToJson;

/**
 * JSON->XJSONの変換
 */
command from-json ::  StandardJson -> StandardXJson
    refers python:xjsonlib.FromJson;


/**
 * XJSONパスを用いてXJSONオブジェクトの項目を取り出す。
 */
command select {@[default(false)] "strict": boolean?} [string pathexp] :: any -> [any*]
    refers python:xjsonlib.Select;

command to-xml :: any -> string
    refers python:xjsonlib.ToXML;

type pathExpr = deferred string; // format="xjson-simple-path"

/** パス式により、データの当該成分を抜き出す */
command get
{
    /** 強制的にsafeパス式とみなす */
    @[default(false)]
    "safe" : boolean?,
    "default": (integer|null|boolean|number|string)?,
} [pathExpr? path_expr] :: univ -> univ
throws [ BadArg, IndexOutOfRange, PropertyNotExist, Undefined, TagNotExist ]
refers python:xjsonlib.Get;

/** パス式により、データの当該成分に値を代入する */
command put 
{
    /** ルーズ配列を許す */
    @[default(false)]
    "allow-loose" : boolean?
} [pathExpr path_expr] :: [univ data, univ val] -> univ
throws [ BadArg, IndexOutOfRange, PropertyNotExist, Undefined, TagNotExist, ConsistencyViolation ]
refers python:xjsonlib.Put;

/**
 * 入力値のXJSONオブジェクトを整形して返す。
 */
command pretty :: any -> string {
    univ-pretty
};

/**
 * 入力値のXJSONオブジェクトを整形して返す。
 * prettyとは違い、undefinedやforeignも出力できる。
 */
command univ-pretty :: univ -> string
    refers python:xjsonlib.Pretty;


/** 配列またはオブジェクトを平坦化する
 * タグ付きの配列／オブジェクトも処理できる。
 * --rec オプションで再帰的に処理する。
 */
command flatten
 {
   /** オブジェクトも処理対象とするか */
   @[default(false)]
   "also-obj": boolean?,

   /** 再帰的に処理するか */
   @[default(false)]
   "rec": boolean?,
 }
 :: @* (array | object) -> @* array 
 
    refers python:xjsonlib.Flatten
 ;

/** undefinedを含んだデータのうち、配列末尾とobjectのundefinedプロパティを削除する。 */
command normalize :: univ -> univ
    refers python:xjsonlib.Normalize;

/**
 * 二つの object をマージする。
 * --mode オプションではプロパティ名が衝突した場合の解消方法を指定する。
 *
 * * "fst": 第一要素のものを用いる
 * * "snd": 第二要素のものを用いる
 * * "error": エラーとして扱い、 null を返す
 */
command merge {@[default("fst")]"mode": string?} :: [object*] -> object | null
    refers python:xjsonlib.Merge;
