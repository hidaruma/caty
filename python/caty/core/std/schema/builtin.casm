/**
 * 組み込みのコマンド/型
 */
module builtin in casm; 

type scalar  = null|boolean|number|string|binary;

/** 
 * JSONの標準データ。
 */
type StandardJson =  JsonScalar | StdArray | StdObject;

type JsonScalar = string | number | null | boolean;

type StdObject = {
  *: StandardJson,
};

type StdArray = [StandardJson*];

/** 
 * foreign, undefinedを含まない標準JSONに変換可能なXJSONデータ。
 */
type StandardXJson = JsonScalar | XArray | XObject | TaggedData;
type TaggedData = @*! JsonScalar | @*! XObject | @*! XArray | @*! TaggedData;
type XArray = [StandardXJson*];
type XObject = {*:StandardXJson?};

@[system, unquiet]
type CatyExecMode = [string*];

@[system]
type DirectoryEntry = {"name":string, "isDir":boolean, *:any};

type ErrorObj = {
    "isError": boolean,
    "orig": string,
    "val": any,
    "message": string,
    "errorId": string?,
    "param": {*:any}?,
    *: any
};

type pinteger = integer(minimum=0);

type ErrorItem = ErrorObj & {
    "name": string,
    *: any
};

type ErrorList = [ErrorItem*];

type ErrorMap = {
    *: ErrorObj
};

type ErrorInfo = {
    "errorList": ErrorList,
    "errorMap": ErrorMap
};

type MiniErrorInfo = MiniErrorObj | MiniError;

type MiniErrorObj = {
    *: string
};

type MiniError = string;

type WebInput = null | string | binary;

// 本当は @form とすべきだろうが使い勝手を考えて
/** HTMLフォームを表現するデータ */
@[unquiet]
type FormInput = {
    *: [WebItem, WebItem*]
};

@[unquiet]
type WebItem = any;

@[unquiet]
type CommonResponsePart = {
    "header": {
        "content-type": string?,
        "content-length": (string|integer)?,
        *: string
    },
    "body": string | binary,
    "encoding": string?, // テキストデータを返すときは必ずこれを追加
    "responseOriginalEncoding" : string?,
};

@[unquiet]
type Response = CommonResponsePart ++ {
    "status": integer(minimum=200, maximum=299),
};

@[unquiet]
type Redirect = {
    "status": integer(minimum=300, maximum=399),
    "header": {
        "Location": string,
        *: string
    }
};

@[unquiet]
type ErrorResponse = CommonResponsePart ++ {
    "status": integer(minimum=400, maximum=599),
};

@[unquiet]
type WebOutput = Response | Redirect | ErrorResponse;

@[unquiet]
type ErrorStatus = {
    "status": integer
};

@[unquiet]
type AppInfo = {
    "name": string,
    "description": string,
    "path": string,
    *:any
};

/**
 * Catyのアプリケーション毎の設定ファイル。
 */
@[system]
type AppManifest = {

    /**
     * 実装言語(現時点ではpythonのみ)
     */
    @[default("python")]
    "implLang": string?,

    /**
     * アプリケーションの説明
     */
    "description": string?,

    /**
     * アプリケーションのより詳しい説明
     */
    "moreDescription": string?,

    /**
     * アノテーション
     */
    "annotations": object?,

    /**
     * アプリケーションの無効化の設定
     */
    "disabled": boolean?,

    /**
     * ディレクトリの末尾に/の付いていないアクセスへの対応。
     * "redirect"を設定すると/の付いたアドレスにリダイレクトされる。
     * "dont-care"では404 Not Foundとなる。
     */
    @[default("redirect")]
    "missingSlash": ("redirect" | "dont-care")?,
    
    /**
     * ファイルタイプとアソシエーションの設定。
     */
    "filetypes": {
        *: FileType ++ {"assoc": Assoc?}
    }?,

    /**
     * アプリケーション固有情報。
     */
    "appProp": CatyAppProperty?,

    /**
     * pub, dataなどのデータの設定。
     */
    "assign": {
        *:string,
    }?,

    /** ディレクトリに対するインデックスファイルの名前 */
    "indexFiles": [string(remark="ファイル名")*]?,

//    *:any
};


/**
 * パッケージの設定ファイル
 */
@[system]
type PkgManifest = {
    /**
     * パッケージの説明
     */
    "description": string?,

    /**
     * パッケージのより詳しい説明
     */
    "moreDescription": string?,

    /**
     * アノテーション
     */
    "annotations": object?,

};



/**
 * Caty全体の設定ファイル。
 */
@[deprecated]
type GlobalConfig = PrjManifest;

/**
 * プロジェクト全体の設定フィアル。
 */
@[system]
type PrjManifest = {
    /**
     * プロジェクト名。省略時はカレントディレクトリの名前が設定される。
     */
    "projectName": string?,

    /**
     * プロジェクトの説明
     */
    "description": string?,

    /**
     * プロジェクトのより詳しい説明
     */
    "moreDescription": string?,

    /**
     * アノテーション
     */
    "annotations": object?,

    /**
     * mafs実装のモジュール名
     */
    "mafsModule": string,

    /**
     * Catyのスタンドアローンサーバのモジュール名
     */
    "serverModule": string,

    /**
     * セッションの設定
     */
    "session": {
        /**
         * セッションのタイプ。"memory"のみ現在は対応。
         */
        "type": string,

        /**
         * セッションの保持される期間
         */
        "expire": integer,
    },

    /**
     * パスワード生成などでsaltの一部に用いる文字列
     */
    "secretKey": string,

    /**
     * システム全体のエンコーディング
     */
    "encoding": string,

    /**
     * Webサーバのアドレス(/は末尾に付けない)
     */
    "hostUrl": string,
    
    /**
     * システムの言語コード
     */
    "language": string,

    /** true にすると Caty スクリプトのコンパイル結果をキャッシュ */
    "enableScriptCache": boolean, 
    
    /** true にすると HTTP メソッドトンネリングが有効になる */
    "enableHTTPMethodTunneling": boolean, 

    /**
     * マルチスレッドの代わりにマルチプロセスを使うかどうか
     * マルチプロセス使用不可の環境では、マルチスレッドにフォールバックされる。
     */
    "useMultiprocessing": boolean, 
    
    /** 
     * Caty アプリケーションとして扱わないディレクトリ名のリスト。デフォルトは["CVS"]
     */
    "ignoreNames": [string*]?, 

    /**
     * アクセス可能なIPアドレスのリスト(末尾のみワイルドカード可)。
     * 特に指定されなかった場合、全てのホストからのアクセスを受け付ける
     */
    "addrsAllowed": [string*]?,

    /**
     * アクセスを拒否するIPアドレスのリスト(末尾のみワイルドカード)
     */
    "addrsDenied": [string*]?,


};

@[system]
type Assoc = {
    // プロパティ名はverb名、デフォルトのverb名（無名）は""
    *:string
};

@[system]
type Assocs = {
    *: Assoc
};

@[system]
type FileType = {
    "isText": boolean?,
    "contentType": string?,
    "description": string?
};

@[system]
type FileTypes = {
    *: FileType
};

type UploadFile = {
    "filename": string,
    "data": binary
};

// Catyの例外データは次の型のサブタイプでなくてはならない
// 実装言語の例外を直接投げることも禁止しない（禁止できない）

/** 人間可読なメッセージ */
type Message = {
  // プロパティ名は言語識別子, ja, en など
  * : string?
} (minProperties = 1);

/** 例外型 Exceptionとダブっているため使用禁止 */
@[deprecated]
type CatyException = @* {
 "message" : Message, 

 // 以下の2つのプロパティは、必要なら分類に使用する
 "class" : string?,
 "id" : (integer|string)?,

 // 実装言語のスタックトレースデータ
 "stackTrace" : any?,
 
  *: any?
};

/**
 * マニフェストにおけるアプリケーション固有情報
 */
type CatyAppProperty = {
    "$typename": string,
    "$callback": string?,
    *: any
};

/**
 * 組み込み型の名前
 */
@[system]
type BuiltinTypeName = (
   "number" | "string" | "boolean" | "null" | "binary" // スカラー型
 | "array" | "object" // 複合型
 | "tagged"  // XJSONによる拡張
 | "undefined" // 未定義
 | "foreign" // フォーリンデータ
);

type typeName = string(remark="type-name");
type uri = string(remark="uri");
type mediaType = string(remark="media-type");
type httpMethod = ("GET" | "PUT" | "POST" | "DELETE" | "HEAD");

type Trigger = {
 // 個々のトリガーを識別する属性
 "id" : string?,
 "name" : string?,
 "class" : string?,

 // 説明的な情報
 "title" : string?,
 "help" : string?,

 // ハイパーリンクの記述
 "href" : uri,   // hrefは必須
 "rel" : string?,
 "rev" : string?,
 "type" : mediaType?,

 "enctype" : mediaType?,
 @[default("GET")]
 "method" : httpMethod?,

 // 手続き呼び出し的なデータ項目
 @[default("")]
 "verb" : string?,
 @[default("void")]
 "inputDatatype": typeName?,
 "paramsDatatype" : typeName?,

 // その他いろいろ
  * : any?
};


type Anchor = Trigger & {
 "enctype" : undefined,
 "method" : "GET",
 "inputDatatype": "void",

  * : any?
};

type Form = Trigger & {
 @[default("application/x-www-form-urlencoded")]
 "enctype" : mediaType?,

  * : any?
};

@[system]
type Exception = @*! {
 "message" : string, 

 // 以下の2つのプロパティは、必要なら分類に使用する
 /** 例外クラス */
 "class" : string?,
 /** 例外ID */
 "id" : (integer|string)?,

 /** 実装言語のスタックトレースデータ */
 "stackTrace" : any?,
 
  *: any?
};

/** 真、YES、成功などを表す型 */
type Positive = (true  | @True univ  | @OK univ);
/** 偽、NO、失敗などを表す型 */
type Negative = (false | @False univ | @NG univ);

/** 不明、不定、未知などを表す */
const indef = @Indef;

/** 三値の真偽値型 */
type triboolean = (true | false | indef);

/** 単一指定はT型データ、複数指定はT型項目の配列を使う型 */
type multiItem<T> = (T | [T, T, T*]);

/** コマンドに複数回オプションを許すときにこの型を使う 
 * multiItem<T> とまったく同じ。
*/
type multiOpt<T> = multiItem<T>;

/** unclose文への入力 */
@[system]
type UncloseInput = UncloseInputObj | UncloseInputArr;

@[system]
type UncloseInputObj = {
  /** 上書きでセットする変数と値 */
  @[default({})]
  "set": object?,

  /** アンセットする変数達 */
  @[default([])]
  "unset": [string*]?,

  /** 入力 */
  @[default(null)]
  "input": univ?,
};

@[system]
type UncloseInputArr = [
  /** 上書きでセットする変数と値 */
  @[default({})]
  object? set,

  /** 入力 */
  @[default(null)]
  univ? input,

  /** アンセットする変数達 */
  @[default([])]
  [string*]? unset,

];


/**********************
 ******** 例外 ********
 **********************/

/*
 * --- NotFound系 --- 
 */

/** 例外：モジュールが存在しない */
exception ModuleNotFound = {
  "moduleName": string,
  "moduleType": "cara" | "casm",
  "appName": string,
  *: any?
};

/** 例外：リソースが存在しない */
exception ResourceNotFound = {
  "moduleName": string,
  "resourceName": string,
  *: any?
};


/** 例外：型が存在しない */
exception TypeNotFound = {
  "moduleName": string,
  "typeName": string,
  *: any?
};

/** 例外：コマンドが存在しない */
exception CommandNotFound = {
  "moduleName": string,
  "cmdName": string,
  *: any?
};

/** 例外：アクションが存在しない */
exception ActionNotFound = {
  "moduleName": string,
  "actionName": string,
  "resourceName": string,
  *: any?
};

/** 例外：ユーザーロールが存在しない */
exception UserroleNotFound = {
  "moduleName": string,
  "userrole": string,
  *: any?
};

/** 例外：アプリケーションが存在しない */
exception ApplicationNotFound = {
  "appName": string,
  *: any?,
};

/** 例外：要求されたスクリプトが見つからない、存在しない。*/
exception ScriptNotFound = object;

/** 例外：要求されたファイルが見つからない、存在しない。*/
exception FileNotFound = object;

/** 例外：データベースのコレクションが見つからない。*/
exception CollectionNotFound = object;

/** 例外：そういう例外型は存在しない
 * 例外型（Excetpion）のデータではあるが、タグが登録されてない。
 */
exception ExceptionNotFound  = {
 /** throwに入力されたデータ */
 "inputData" : Exception,
 *: any?
};



/* 
 * --- 不正な操作／壊れたデータ --- 
 */

/** 例外：データの整合性を破壊するので許されない操作である。*/
exception ConsistencyViolation = object;

/** 例外：配列のインデックスが範囲外、または不正である。*/
exception IndexOutOfRange = object;

/** 例外：オブジェクトのプロパティが存在しない、またはプロパティ名が不正である。*/
exception PropertyNotExist = object;

/** 例外：データが（事故や不注意で）壊れていると推測される。*/
exception MalformedData = object;

/** 例外：構文エラーが存在する。パーシングが失敗した。*/
exception SyntaxError = object;

/** 例外：データの変換に失敗した */
exception ConversionError = {
    "errorObj": MiniErrorInfo,
    "source": univ,
    "type": string?,
    *: any?
};

/** 例外：型の妥当性検証に失敗した。*/
@[runaway]
exception ValidatatonFailed = object;

/** 例外：入力が（なんらかの意味で）不正である。（旧） */
@[deprecated]
exception InvalidInput = object;

/** 例外：入力が（なんらかの意味で）不正である。*/
exception BadInput = object;

/** 例外：ルーズ配列である。 */
exception LooseArray = object;

/** 例外：未定義である。 */
exception Undefined = object;

/** 例外：タグが存在しない。 */
exception TagNotExist = object;


/* 
 * --- IOエラー
 */

/** 例外：ファイルのIOに関連してエラーが生じた。 */
exception FileIOError = object;

/** 例外：コレクションへのアクセスに失敗。*/
exception CollectionAccessError = object;


/* 
 * --- 不正なコマンドライン --- 
 */

/** 例外：指定できないオプションが指定された。*/
@[runaway]
exception UnexpectedOption = object;

/** 例外：オプションの値が不正である。*/
@[runaway]
exception BadOption = object;

/** 例外：オプションが不足している。*/
@[runaway]
exception MissingOption = object;

/** 例外：引数が余分にある。*/
@[runaway]
exception UnexpectedArg = object;

/** 例外：引数が不足している。*/
@[runaway]
exception MissingArg = object;

/** 例外：引数の値が不正である。 */
@[runaway]
exception BadArg = object;

/*
 * -- ユーザーの誤り
 */

/** 例外：throwへの入力が不正 */
@[runaway]
exception InvalidThrow = {
 /** throwに入力されたデータ */
 "inputData" : univ,
 *: any?
};

/** 例外：シグナルがハンドルされなかった */
@[runaway]
exception UnhandledSignal = {
 "signal" : univ,
 *: any?
};

/** 例外：uncloseでsetとunsetの競合が起きた。 */
@[runaway]
exception UncloseConflict = object;


/*
 * --- セキュリティ
 */

/** 例外：認証失敗 */
exception CertificationFailed = object;

/** 例外：不正なアクセス */
exception IllegalAccess = object;

/** 例外：その他セキュリティエラー。 */
exception SecurityError = object;

/*
 * --- その他 ---
 */

/** 例外：コマンド実行エラー */
exception CommandExecutionError = object;

/** 例外：サポートされていない操作。 */
exception Unsupported = object;

/** 例外：タイムアウト。 */
exception Timeout = object;

/** 例外：算術エラー。 */
exception ArithmeticError = object;

/** 例外：Pythonレベルの実行時エラー。 */
exception RuntimeError = object;

/** 例外：未実装。 */
exception NotImplemented =object;

/** 例外：不明なエラー。*/
exception UnknownError = object;

/** 例外：実装のバグ。 */
exception ImplementationBug = object;

/** 例外：コンパイルエラー */
exception CompileError = object;

/** 例外：neverが検出された */
exception Never = object;


/** 
 * 入力値を例外として送出する。
 * 入力値が例外型でなかった場合はInvalidThrow例外が、
 * 例外型ではあるが未定義だった場合はExceptionNotFound例外が送出される。
 */
command throw :: any -> never throws Exception
    refers python:caty.core.std.command.builtin.Throw;

/** 
 * 入力値をシグナルとして送出する。
 */
command signal {@[default(false)]"runaway": boolean?} :: any -> never signals any
    refers python:caty.core.std.command.builtin.Signal;

/**
 * テンプレートを展開する際に扱われるデータ。
 * expand, printなどでテンプレートが展開される際、
 * 入力がobjectの場合はこの型のデータがプロパティとして追加される。
 * 入力がobjectでない場合は、入力をCONTEXTの値としたobjectとなる。
 *
 */
@[system]
type DefaultTemplateContext = {
    /** expand, printの引数のファイルパス */
    "_FILE_PATH": string,

    /** APP_PATH環境変数と同じ */
    "_APP_PATH": string,
    
    /** HOST_URL環境変数と同じ */
    "_HOST_URL": string,

    /** CATY_VERSION環境変数と同じ */
    "_CATY_VERSION": string,

    /** print, expandへの実際の入力 */
    "CONTEXT": any,
    *: any?
};

/**
 * 入力値を標準出力に書き出す。それ以外の動作は pass と同じ。
 */
command dump<T default univ> // 実は総称、passと同じだから
 {
  /** debugフラグに関わらず表示を強制する */
  @[default(false)]
  "force" : boolean?,

  /** 表示の先頭に付加する文字列 */
  @[default("")]
  "prefix" : string?,
 }
 :: T -> T
  reads env
    refers python:caty.core.std.command.debug.Dump;

/**
 * 簡易的な print コマンド。
 *
 * print と同様の働きをするが、出力が HTTP 応答ヘッダのない単なる文字列となる。
 * コマンドラインシェルから起動する事を前提に作られている。
 * バイナリデータを読み込んだ場合、特に加工せずそのまま出力する。
 *
 */
command expand {"raw": boolean?, "no-script": boolean?, "resolve": boolean?, "mode":string?} [string filepath] :: 
    any -> string | binary
    uses [pub, env, include, interpreter]
    reads [schema]
    refers python:caty.core.std.command.builtin.Expand;

/*{{{
/** テンプレートをファイルからではなく文字列から取るexpand */
command expand_2 {"raw": boolean?, "no-script": boolean?, "resolve": boolean?, "mode":string?} :: 
    [any, string] -> string | binary
    uses [pub, env, include, interpreter]
    reads [schema]
    refers python:caty.core.std.command.builtin.Expand2;
    }}}*/

/**
 * 入力値を HTTP の応答ボディとし、 HTTP 応答ヘッダを付けて返す。
 * --ext オプションは拡張子の指定であり、このオプションが指定された場合は content-type ヘッダが対応した値となる。
 * 指定されなかった場合は application/octet-stream が用いられる。
 */
command response {"ext":string?, 
                  "status": integer?, 
                  "content-type": string?, 
                  "encoding": string?} :: string | binary -> Response
    reads [env, pub]
    refers python:caty.core.std.command.builtin.Response;

/**
 * 入力をテンプレートに対して適用し、 HTTP 応答ヘッダを付けて返す。
 *
 * --raw オプションが指定された場合、テンプレートを評価せずにそのまま返す。
 *
 * --resolve オプションが指定された場合、インライン/アウトオブラインスクリプトをテンプレートの入力として扱う。
 * 両者が同時に指定された場合、インラインスクリプトが優先して使用される。
 *
 * --mode オプションは text か html のどちらかを値としてとる。
 * text が指定された場合、 HTML 特殊文字の自動エスケープが働かなくなる。
 * デフォルトでは html が指定された状態であり、 Web ページの出力にはこちらを使う。
 * text はメールの文面の出力など、 Web ページの出力以外の場合に用いること。
 */
command print {"raw": boolean?, 
               "no-script": boolean?,
               @[default(true)]
               "resolve": boolean?, 
               "mode":string?} [string filepath] :: 
    any -> Response
    uses [pub, include, interpreter, env]
    reads [schema]
    refers python:caty.core.std.command.builtin.Print;

/**
 * Webアクセスをエミュレートする。
 * 
 *
 */
command request {
                "verb": string?, 
                "method": string?,
                "debug": boolean?,
                "fullpath": boolean?,
                "content-type": string?} [string path, string? query] :: WebInput -> WebOutput
    reads [env, schema]
    uses interpreter
    refers python:caty.core.std.command.builtin.Request;

/**
 * 引数で指定された型に入力値を変換して返す。
 */
command translate {"content-type": string?} [string] :: WebInput -> @OK any | @NG MiniErrorInfo
    reads [env, schema]
    refers python:caty.core.std.command.builtin.Translate;

/**
 * 引数で指定された型で入力値を検証する。
 */
command validate<T default univ> {"mod": string?, "boolean": boolean?} [string?] :: univ -> @OK univ | @NG MiniErrorInfo | boolean
    reads schema
    refers python:caty.core.std.command.builtin.Validate;

/**
 * 入力を捨てるコマンド。
 * 
 */
command void<T default univ> :: T -> void
  refers python:caty.core.std.command.builtin.Void;

/**
 * バージョン情報の表示。
 *
 */
command version :: void -> string 
  reads env
  refers python:caty.core.std.command.builtin.Version;

/**
 * ディレクトリ一覧の表示。
 *
 * 第一引数で指定されたディレクトリを読み込み、配下のファイルとディレクトリ一覧を返す。
 * 第二引数は拡張子の指定であり、例えば .html が指定された場合 .html 拡張子のファイル一覧だけが返される。
 * 通常はファイル名とファイルかディレクトリの種別のみが買えされるが、
 * --long オプションが指定された場合は最終更新時刻なども返される。
 */
@[deprecated]
command lsdir {"long":boolean?, "kind":string?} [string, string?] :: 
    void -> [DirectoryEntry*]
    reads pub
    refers python:caty.core.std.command.file.LsDir;

/**
 * 入力をそのまま出力にコピーする。
 *
 */
@[no-auto-fill]
command pass<T default univ> :: T -> T
    refers python:caty.core.std.command.builtin.PassData;

/**
 * 入力がundefinedのとき、引数の値に置き換える。
 */
command default [any value] :: univ -> univ
    refers python:caty.core.std.command.builtin.Default;

/**
 * 配列の n 番目のデータを返す。 item との違いはこちらは 1 始まりということである。
 * 値が存在しなかった場合は：\\
 * safeオプションが指定されてないなら、実行時エラーとなる。\\
 * safeオプションが指定されていれば、undefined値を返す。
 *
 * safeオプションが指定されているなら、undefined値の入力をそのままpassする。
 */
command nth<T default any>
 {
   @[default(false)]
   "safe": boolean?,
 }
 [integer n]
 :: (@* [T*] | undefined) -> T?
    refers python:caty.core.std.command.builtin.Nth;

/**
 * 配列の添字 n のデータを返す。 nth との違いはこちらは 0 始まりということである。
 * 値が存在しなかった場合は：\\
 * safeオプションが指定されてないなら、実行時エラーとなる。\\
 * safeオプションが指定されていれば、undefined値を返す。
 *
 * safeオプションが指定されているなら、undefined値の入力をそのままpassする。
 */
command item<T default any>
 {
   @[default(false)]
   "safe": boolean?,
 }
 [integer i] 
 :: (@* [T*] | undefined) -> T?
    refers python:caty.core.std.command.builtin.Item;

/**
 * JSON オブジェクトから引数で与えられた名前のプロパティの値を取得する。
 * 値が存在しなかった場合は：\\
 * safeオプションが指定されてないなら、実行時エラーとなる。\\
 * safeオプションが指定されていれば、undefined値を返す。
 * 
 * safeオプションが指定されているなら、undefined値の入力をそのままpassする。
 */
command pv<T default any> 
 {
   @[default(false)]
   "safe": boolean?,
 }
 [string propName] 
:: (@* object | undefined) -> T?
   refers python:caty.core.std.command.builtin.GetPV;

/**
 * JSON オブジェクトから引数で与えられた名前のプロパティの値を取得する。
 * 値が存在した場合 @EXISTS タグがついた値が返り、そうでなければ @NO タグのついた null が返る。
 */
@[deprecated]
command findpv<T default any> [string] :: object -> @EXISTS T | @NO null
        refers python:caty.core.std.command.builtin.FindPV;
    
/**
 * 二つの入力値の比較を行う。
 */
command eq 
 {
   @[default(false)]
   "boolean": boolean? 
 }
:: [univ, univ] -> (
     // --taggedオプションが指定されたとき
     @True [univ, univ] | @False [univ, univ] |
     // --booleanオプションが指定されたとき
     boolean
   )
    refers python:caty.core.std.command.builtin.Equals;

/**
 * 入力値二つのうち第一要素を Caty スクリプトの式、第二要素をその入力として実行する。
 * このコマンドはテストモードでしか動作しないため、通常は使う必要はない。
 */
@[test] command eval<T default univ> :: [any, string] | string -> T
    reads env
    uses interpreter
    refers python:caty.core.std.command.builtin.Eval;

/**
 * ディレクトリへのアクセスをindex.html, index.cgi, index.actに振り分ける。
 * このコマンドではverbを用いたアクセスをインデックスファイルに行えない。
 * もしもindex.htmlなどにverb付きアクセスをしたいのであれば、ディレクトリアクセスのアクションも書くこと。
 */
command dir-index [string path, string method] :: any -> any
    uses interpreter
    refers python:caty.core.std.command.builtin.DirIndex;

/**
 * 入力値のタグを取得する。組み込み型が与えられた場合、その型名がタグとして扱われる。
 */
command tag<T default univ> :: T -> string
    refers python:caty.core.std.command.builtin.GetTag;

/**
 * 第一の入力値をタグ名とし、第二の入力値をタグ付きにして返す。
 */
command tagged<S default univ, T default any> :: [string, S?] -> T
    refers python:caty.core.std.command.builtin.Tagged;

/**
 * 入力値のタグを除去して返す。組み込み型が与えられた場合、その値がそのまま返る。
 * 引数が与えられた場合、タグ名と引数を比較し、異なっていた場合は例外を創出する。
 */
command untagged<S default any, T default univ> [string?] :: S -> T
    refers python:caty.core.std.command.builtin.Untagged;

/**
 * コンソールに入力値を書き出す。
 * --no-nl オプションが指定された場合、改行を出力しない。
 */
@[console] command cout {"no-nl" : boolean?}:: (string|binary) -> void
    reads env
    updates stream
    refers python:caty.core.std.command.builtin.ConsoleOut;

/**
 * 標準入力より1行を読み込み、それを文字列として返す。
 */
@[console, interactive]
command cin [string? prompt] :: void -> string
    reads env
    refers python:caty.core.std.command.builtin.ConsoleIn;

/**
 * 現在のアプリケーションのアプリケーション情報を返す。
 */
command app :: void -> AppInfo
    updates stream
    reads env
    refers python:caty.core.std.command.builtin.DisplayApp;

/**
 * すべてのアプリケーションのアプリケーション情報を返す。
 */
command apps :: void -> [AppInfo*]
    reads env
    refers python:caty.core.std.command.builtin.DisplayApps;

/**
 * 現在の作業ディレクトリ名を返す。
 * --long オプションが指定された場合、フルパスでの表示となる。
 */
@[console, deprecated] command home {"long":boolean?} :: void -> string
    reads env
    refers python:caty.core.std.command.builtin.Home;

/**
 * 現在の作業ディレクトリ名を返す。
 */
command location :: void -> string
    reads env
    refers python:caty.core.std.command.builtin.Location;

/**
 * プロジェクト名を返す。
 */
command proj :: void -> string
    reads env
    refers python:caty.core.std.command.builtin.Project;

/**
 * 現在のアプリケーションのマニフェストを返す。
 * これは通常 _manifest.json に書かれている情報である。
 * _manifest.json が存在しない場合、 Caty が自動生成した設定が表示される。
 */
@[console] command manifest :: void -> AppManifest
    reads env
    refers python:caty.core.std.command.builtin.Manifest;

/**
 * ファイルに関連付けられたスクリプトを実行する。
 *
 */
@[console] command exec-context<S default any, T default any> [string] :: S -> T
    uses [pub, include, interpreter]
    refers python:caty.core.std.command.builtin.ExecContext;

/**
 * ファイルに対する関連付けを出力する。
 *
 */
@[console] command assoc  
                       :: void -> Assocs
                       {} [string]         :: void -> Assoc
                       {} [string, string] :: void -> string | null
    reads env
    refers python:caty.core.std.command.builtin.Assoc;

/**
 * ファイルタイプを出力する。
 *
 *
 */
@[console] command filetype :: void -> FileTypes
                          {} [string] :: void -> FileType | null
        reads [pub, env]
        refers python:caty.core.std.command.builtin.ShowFileType;

/**
 * リダイレクト処理。引数のパスに対してそのままリダイレクトする。
 */
command redirect [string] :: void -> never
                 :: string -> never
    signals Redirect
    reads env
    refers python:caty.core.std.command.builtin.Redirect;

/**
 * スクリプトローカルスキーマの定義。
 * CatyFIT で使うためのコマンドなので、ユーザが明示的に使う事は想定していない。
 */
command define-local-schema :: string -> void
    reads schema
    refers python:caty.core.std.command.builtin.LocalSchema;

/**
 * バイナリデータへの変換。
 * 引数でフォーマットを指定し、入力文字列をそのフォーマットのバイナリと解釈して返す。
 * 特に指定しなかった場合、 base64 と解釈される。
 */
command binary [("base64"|"raw")?] :: string -> binary
    refers python:caty.core.std.command.builtin.Binary;

/**
 * フォーリンデータを出力する。
 */
command foreign :: void -> foreign
    refers python:caty.core.std.command.builtin.Foreign;


/**
 * 環境変数の一覧の出力
 */
command env :: void -> object
    reads env
    refers python:caty.core.std.command.builtin.ListEnv;

/**
 * JSON オブジェクトのプロパティ名のリストを返す。
 */
command properties :: object -> [string*]
    refers python:caty.core.std.command.builtin.Properties;

/** 入力の型を判断する */
command typeof :: univ -> BuiltinTypeName 
    refers python:caty.core.std.command.builtin.TypeOf;

/** 入力の型が引数で指定された名前の型であるかどうかを判断する */
command typeis [BuiltinTypeName] :: any -> boolean
refers python:caty.core.std.command.builtin.TypeIs;

/**
 * undefined値を出力する。
 *
 */
command undefined :: void -> undefined
    refers python:caty.core.std.command.builtin.Undef;
    
/** リクエスト情報からスクリプトコードを選択する。
 * method, verb, path からディスパッチされるスクリプトのテキストを
 * stringデータとして出力する。
 * 適切なスクリプトがないときはnullを出力する。
 */
command select-script {
    "method" :  ("GET"|"POST"|"PUT"|"DELETE"|"HEAD")?,
    "verb" : string?,
    "fullpath": boolean?,
    "exception": boolean?,
    "check": boolean?,
    } [string] :: void -> (string | null)
    refers python:caty.core.std.command.builtin.SelectScript;


/** リクエスト情報からアクションを選択する。
 * method, verb, path からディスパッチされるスクリプトのテキストを
 * stringデータとして出力する。
 * 適切なアクションがないときはnullを出力する。
 */
command select-action {
    "method" :  ("GET"|"POST"|"PUT"|"DELETE"|"HEAD")?,
    "verb" : string?,
    "fullpath": boolean?,
    "exception": boolean?,
    "check": boolean?,
    } [string] :: void -> (string | false)
    refers python:caty.core.std.command.builtin.SelectAction;

type _trace = [(string|null|boolean)*](minItems=1);

/** リクエストに対してマッチしたリソースクラス名を試行した順番に出力する */
@[deprecated]
command trace-dispatch
{
  @[default("GET")]
  "method" : ("GET"|"POST"|"PUT"|"DELETE"|"HEAD")?,
  @[default(false)]
  "fullpath": boolean?,
  "verb"   : string?,
  "check": boolean?,
} [string path]
:: void -> @EXISTS _trace | @FAILED _trace
    refers python:caty.core.std.command.builtin.TraceDispatch;

@[unquiet]
type HelpInfo = deferred @* object;

/**
* モジュール/コマンド/型のヘルプを表示する。
* 型のヘルプを出力する時は--typeオプションを、コマンドのヘルプを出力する時は--commandオプションをつける。
* そのどちらも付けられなかった場合、このヘルプが表示される。
* 引数のフォーマットは以下の通り。
* 
* {{{
*     *               ビルトインモジュールのコマンド一覧を表示
*     command         ビルトインモジュールの command のヘルプを表示
*     *:              モジュール一覧を表示
*     module:*        module のコマンド一覧を表示
*     module:command  module:command のヘルプを表示
* }}}
* 
* これはコマンドのヘルプのフォーマットであるが、型のヘルプも同様である。
* オプションの--jsonが指定された場合、JSON形式のヘルプが返る(未実装)。
*
* == コマンド特有のオプション
*
* --filterオプションはコマンド一覧を出力する際、
* 出力するコマンドをfilterに限定するために用いる。
*
* == 型特有のオプション
*
* --exceptionオプションは型一覧を出力する際、
* 出力する型を例外型に限定するために用いる。
*
* == コンソールでのみ有効な特殊コマンド
* 
* change APP_NAME  : アプリケーションをAPP_NAMEに変更する。短縮コマンドはch, cd
* quit             : Catyシェルを終了する。
* reload           : グローバル設定とアプリケーションの再読み込み。短縮コマンドはl
* server start PORT: PORTでCatyサーバを稼働させる。
*        stop      : Catyサーバを停止する。
*/
command help
{
  @[default(false), without(["type", "resource"])]
  "command" : boolean?,

  @[default(false), without(["command", "resource"])]
  "type" : boolean?,

  @[default(false), without(["command", "type"])]
  "resource" : boolean?,

  // 以下は、既存のhelp, typeのオプション
  
  @[default(false), not-implemented]
  "json": boolean?,

  @[default(false), with("command")]
  "filter": boolean?,

  @[default(false), with("type")]
  "exception": boolean?,

} [string? pattern] :: void -> (string | HelpInfo)
  reads [interpreter, schema]
  refers python:caty.core.std.command.builtin.Help;

command hc {

  @[default(false)]
  "filter": boolean?,

} [string? pattern] :: void -> void {
  help --command %--filter %1 | cout
};

command ht {

  @[default(false)]
  "exception": boolean?,

} [string? pattern] :: void -> void {
  help --type %--exception %1 | cout
};

command hr {
} [string? pattern] :: void -> void {
  help --resource %1 | cout
};

command h :: void -> void {
    help | cout
};

/** 例外データを作る。
 * tagが既存の例外型に対応してないときは、ExceptionNotFound例外。
 */
command make-exception [string tag, string message] :: void -> Exception
    throws ExceptionNotFound
    reads schema
    refers python:caty.core.std.command.builtin.MakeException;

/**
 * [文字列, データ]のペアの配列からオブジェクトを作る。
 * 上記のペアの第一要素がオブジェクトのプロパティとなる。
 */
command array-to-object {@[default(false)]"multi": boolean?} :: [[string, any]*] -> object
    refers python:caty.core.std.command.builtin.ArrayToObject;

/**
 * オブジェクトから[文字列, データ]のペアの配列を作る。
 */
command object-to-array {@[default(false)]"multi": boolean?} :: object -> [[string, any]*]
    refers python:caty.core.std.command.builtin.ObjectToArray;

/** 
 * 引数のコマンドあるいはCatyScriptを呼び出す。
 */
command call<S default univ, T default univ> [string command_name] :: S -> T
    reads [pub, scripts, interpreter]
    refers python:caty.core.script.interpreter.executor.CallCommand;

/** 
 * 引数のコマンドあるいはCatyScriptを呼び出し、制御をそちらに移す。
 */
command forward<S default univ, T default univ>  [string command_name] :: S -> never
    signals T
    reads [pub, scripts, interpreter]
    refers python:caty.core.script.interpreter.executor.Forward;

/**
 * 引数で指定されたミリ秒だけ停止する。
 * 引数が省略された場合は1秒(1000ミリ秒)停止する。
 */
command sleep<T default any> [integer(minimum=0)? millisec] :: T -> T
    refers python:caty.core.std.command.builtin.Sleep;
   
/** 
 * 入力データを文字列化する。
 */
command to-string :: any -> string
    refers python:caty.core.std.command.builtin.ToString;

/** 
 * 何も出力せず、Never例外を投げる。
 */   
command never :: void -> never throws Never
    refers python:caty.core.std.command.builtin.Never;

/** 型のデフォルト値を埋めた値を返す。 */
command fill<T default univ> :: T -> T
    refers python:caty.core.std.command.builtin.Fill;

command keys :: array | object | @*! (array|object) -> [(string|integer)*] {
    untagged | each {%_key}
};

/** 入力が純粋JSONに変換不可能なときは例外またはundefined値とする
 * 変換不可能な値とは：
 *
 * * binary
 * * undefined
 * * foreign
 * * これらを含む複合型（タグオンリーはundefinedを含む）
 *
 * タグは変換できる。
 */
command reject-non-json
 {
   /** 例外の代わりにundefineを出力するか */
   @[default(false)]
   "safe": boolean?
 } :: univ -> any? throws Unsupported {
  $? > in;
  %in? | validate<StandardXJson> | when {
    OK => %in,
    NG => %safe | case {true => undefined, false => @Unsupported {"message": "$data", "data": %in?} | throw}
  }
};

/* アノテーション宣言 */
/** アノテーションのターゲットタイプ 
 */
@[system]
type TargetType = (
  /** 任意のターゲット */
  "ANY_TARGET" |
  /** 型の宣言文 */
  "TYPE_DECL" | 
  /** 例外型の宣言文 */
  "EXCEPTION_DECL" | 
  /** スカラー型 */
  "SCALAR" |
  /** オブジェクト型 */ 
  "OBJECT" |
  /** 配列型 */
  "ARRAY" |
  /** ユニオン型 */
  "UNION" |
  /** タグ付き型 */
  "TAGGED" | // TAGGED_TYPE から変更
  /** 任意の型（型表現） */
  "TYPE" |
  /** オブジェクトのプロパティ */
  "PROPERTY" |
  /** 配列の項目 */
  "ITEM" |
  /** 列挙型／ユニオン型の選択肢 */
  "CHOICE" |
  /** コマンドの宣言文 */
  "COMMAND_DECL" |
  /** コマンドのオプション（プロパティ） */
  "OPTION" |
  /** コマンドの引数（項目） */
  "ARGUMENT" |
  /** アノテーションの宣言文 */
  "ANNOTATION_DECL" |
  /** アノテーションのパラメータ（プロパティ） */
  "ANNOTATION_PARAM" |
  /** モジュールの宣言文（module 名前 ;） */
  "MODULE_DECL" | 
  /** 定数の宣言文 */
  "CONST_DECL" |
  /** クラスの宣言文 */
  "CLASS_DECL" |
);

/** アノテーションのターゲットを指定
 * どの構文構成素に対してアノテートするかを指定する。
 * このアノテーションが指定されないときは、ANY_TARGET となる。
 */
@[target("ANNOTATION_DECL")]
annotation target = {
 "value" : (TargetType | [TargetType, TargetType*])
};

@[target(["COMMAND_DECL", "TYPE_DECL", "CONST_DECL", "MODULE_DECL", "CLASS_DECL"])]
annotation register-public = {"value": boolean};

@[target(["COMMAND_DECL", "TYPE_DECL", "CONST_DECL", "MODULE_DECL", "CLASS_DECL"])]
annotation override-public = {"value": boolean};

@[target(["COMMAND_DECL", "TYPE_DECL", "CONST_DECL", "MODULE_DECL", "CLASS_DECL"])]
annotation override = {"value": boolean};

@[target("PROPERTY")]
annotation without = {
  "value" : (string | [string, string*])
};

@[target("PROPERTY")]
annotation with = {
  "value" : (string | [string, string*])
};

@[target(["PROPERTY", "TYPE", "ITEM"])]
annotation default = {
  "value" : univ 
};

@[target(["COMMAND_DECL", "TYPE_DECL", "CONST_DECL", "MODULE_DECL", "CLASS_DECL", "ITEM", "PROPERTY"])]
annotation deprecated = {
  "value" : boolean
};

@[target("COMMAND_DECL")]
annotation filter = {
  "value": boolean
};

@[target("COMMAND_DECL")]
annotation no-auto-fill = {
  "value": boolean
};

@[target("COMMAND_DECL")]
annotation interactive = {
  "value": boolean
};

