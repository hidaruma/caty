module collection;

facility default-storage (string database-name)
   applying (string collection-name)
   conforms Collection
   refers python:storage.DefaultStorage;

master default-database = default-storage("default-database");
entity default-collection = default-database("default-collection");

/** デフォルトのレコード型 
 */
@[register-public]
type DefaultRecord = {
 /** 主キーは id という名前の文字列フィールドである */
 "id": string?, 

 /* 他は任意 */
 *:any?
};


kind KeyType = lower (number | string | binary);
kind FragmentPathType = lower string?;
kind RecordType = lower object;

type Iterable<T> = ([T*] | foreign);


/** 
 * コレクションを操作するコマンドセット 
 */
@[register-public]
signature Collection
<
  /** レコードの型 */
  Record in RecordType default DefaultRecord,

  /** 主キー（IDフィールド）の型 */
  Key in KeyType default string,

  /** フラグメントパスの型
   * string または undefined に具体化して使う。
   */
  FragmentPath in FragmentPathType default string,
> =
{
 /** 主キー（IDフィールド）の型の名前
  */
  property keyType :: string(remark="キーの型の名前") ;

 /** レコードの型の名前
  */
  property recordType :: string(remark="キーの型の名前") ;


 /** 引数に与えられたキーにより単一レコードを取得する */
 @[reader]
 command lookup [Key key] :: void -> Record
         throws NotFound;

 /** 引数に与えられたキーとフラグメントパスからレコードポーションを得る 
  * フラグメントが返るので、出力型は動的。
  */
 @[reader]
 command get [Key key, FragmentPath? fragment-path] :: void -> any
         throws NotFound;

 /** レコードがコレクションに所属（∈）するかどうか判定する 
  * IDだけでなく、値の同一性もチェックする。
  */
 @[reader]
 command belongs :: Record -> boolean;

 /** キーに対応するレコードが存在するかどうか判定する */
 @[reader]
 command exists [Key key] :: void -> boolean;

 /** すべてのキーを列挙する */
 @[reader]
 command keys :: void -> Iterable<Key>; // eachの入力

 /** すべてのレコードを列挙する */
 @[reader]
 command all :: void -> Iterable<Record>; // eachの入力

 /** コレクションにレコードを挿入する 
  * 引数にキーが指定されれば、そのキーを使う。この場合、入力データであるレコードにキーがあっても無視される。
  *
  * 引数にキーが指定されず、レコードにもIDがないきは可能なら自動生成する、
  * 自動生成が不可能ならBadInput例外。
  */
 @[updater]
 command insert [Key? key] :: Record -> Record
   throws [AlreadyExists, BadInput]
 ;

 /** 単一レコードを置き換える 
  * 必ず既存レコードを置き換える。
  */
 @[updater]
 command replace [Key key] :: Record -> Record
   throws NotExist
 ;

 /** 引数に指定されたキーのレコードを削除する 
  * 出力は任意である。
  * 削除に失敗した場合は例外とする。
  */
 @[updater]
 command delete [Key key] :: void -> any
   throws NotFound
 ;

 /* == デフォルト実装付きのコマンドプロファイル == */

 /** 入力に与えられたキーとフラグメントパスからレコードポーションを得る */
 command get_ :: [Key, FragmentPath?] -> any throws NotFound {
   =[ pass > k, pass > p];
   get %k %p?
 };

 /** 存在すればレコードを取得し、そうでないならundefinedを返す */
 @[reader]
 command get-if-exists [Key key, FragmentPath? fragment-path] :: void -> Record? {
   exists %1 |
   case {
    true  => get %1 %2?,
    false => undefined,
   }
 };

 /** 存在すればレコードを置換し、そうでないなら新規作成する */
 @[updater]
 command put [Key? key] :: Record -> Record {
   pass > record;
   %1? |
   when {
     undefined => 
       %record | insert // -> output
       ,
     * => 
       pass > id; exists %id |
       case {
         true  => %record | replace %id,  // -> output
         false => %record | insert %id,   // -> output
       }
       ,
   }
 };
} refers python:collection;




@[hidden] signature StorageAdmin = {
/*


|コマンド                |入力                    | 別名   |
|------------------------|------------------------|--------|
| create-database 名前?  |DatabaseOptions         |        |
| activate-database 名前?|ActivationOptions       |connect |
| confirm-database 名前? |ConfirmOptions          |confirm |
| list-collecions 名前?  |void                    |list    |
| drop-database 名前?    |void                    |        |
| create-collection 名前 |CollectionOptions       |        |
| drop-collection 名前   |void                    |        |
| clear-collection 名前  |void                    |truncate|
| alter-collection 名前  |CollectionOptions       |        |
| show-collectin 名前    |void                    |describe|

*/

 type DatabaseOptions;
 type CollectionOptions;

 type DatabaseInfo;
 type CollectionInfo;

 type ConfirmOptions;
 type ActivationOptions;


 /** データベースを作成する
  * 1つのストレージバックエンドが1つのデータベースしかサポートしない場合は、
  * データベース名は不要。
  */
 command create-database [string? database-name] 
  :: DatabaseOptions -> any
 ;

 /** 存在するデータベースをアクティブ化する
  * データベースサーバーへの接続処理などを行う。
  */
 command activate-database [string? database-name]
  :: ActivationOptions -> any
 ;
 command connect := activate-database;


 /** データベースを利用可能とする
  * 存在しなければ作成し、アクティブでないならアクティブ化する。
  */
 command confirm-database [string? database-name]
  :: ConfirmOptions -> any
 ;
 command confirm := confirm-database;


 /** データベース内のすべてのコレクションを列挙する */
 command list-collecions [string? database-name]
  :: void -> [CollectionInfo*]
 ;
 command list := list-collections;

 /** データベースを削除する */
 command drop-database [string? database-name]
  :: void -> any
 ;


 /** コレクションを新規作成する */
 command create-collection [string? database-name] 
  :: CollectionOptions -> any
 ;

 /** コレクションを削除する */
 command drop-collection [string? database-name]
   :: void -> any
 ;

 /** コレクションに含まれるすべてのレコードを削除して空にする */
 command clear-collection [string? database-name]
  :: void -> any
 ;
 command truncate := clear-collection;

 /** コレクションに関する設定を変更する */
 command alter-collection [string? database-name]
  :: CollectionOptions -> any
 ;

 /** コレクションに関する情報を出力する */
 command show-collectin [string? database-name]
  :: void -> CollectionInfo
 throws NotFound
 ;
 command describe := show-collection;

};

@[__collection, __identified("id"), __id-type("string")]
type default-collection = {
    "id": string?,
    *: any?
};

class default-collection conforms Collection<default-collection, string> = {};
