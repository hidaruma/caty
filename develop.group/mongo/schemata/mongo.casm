/** 
 * mongodb操作用モジュール
 * JSONストレージバックエンドとは別個に作ってある。
 */
module mongo in casm;

/** _tagプロパティを使用禁止にしたオブジェクト */
type BsonizableUntaggedObject = {
 /** _tagは特別な目的に予約する */
 "_tag": undefined,
 * : BsonizableData?
}(propNameFormat="bson-key");


/** オブジェクトにタグを許す */
type BsonizableObject = @* BsonizableUntaggedObject;

/** 
 * mongodbに格納される形の値に変換する。
 */
command to-bson :: BsonizableObject -> BsonizedData
    refers python:mongo.ToBSON;

type BsonizableData =(string | number | boolean | EncodedObjectId | BsonizableObject | binary);

type ObjectId = foreign;


type BsonizedObject = {
 "_tag": string?,
 * : BsonizedData?
}(propNameFormat="bson-key");

type BsonizedData = (BScalar | BsonizedObject | ObjectId);

type Record = {
    *:(BScalar | Record | BArray)?
};
type BScalar = string | number | boolean | EncodedObjectId;
type BArray = [(BScalar|BArray|Record)*];
type QueryExpr = query:Pred | query:ObjSelector;
type EncodedObjectId = @ObjectId string;


@[signature]
class MongoHandler(foreign) {
};

type MongoConfig = {
    "host": string,
    "port": integer,
};

facility MongoHandler(null) configured MongoConfig
    conforms MongoHandler
    refers python:mongolib.MongoHandler;

entity mongo = MongoHandler(null);

type Status = {
  "status": ("stopped" | "running" | "unknown"),
  "host": string(remark="ホストアドレス"),
  "port": integer(remark="ポート番号"),
};

command status :: void -> Status
    reads mongo
    refers python:mongo.Status
;

command list-databases :: void -> [string*]
    uses mongo
    refers python:mongo.ListDatabases
;

command create-database [string] :: void -> Database
    uses mongo
    refers python:mongo.CreateDatabase
;

command activate-database [string] :: void -> Database
    uses mongo
    refers python:mongo.ActivateDatabase
;

@[signature]
class Database(Database) {
    @[reader]
    command list-collections :: void -> [string*]
        refers python:mongo.ListCollections;

    @[updater]
    command drop-collection [string] :: void -> void
        refers python:mongo.DropCollection;
    
    @[updater]
    command create-collection [string] :: void -> Collection
        refers python:mongo.CreateCollection;
    
    @[updater]
    command clear-collection [string] :: void -> void
        refers python:mongo.ClearCollection;
};

@[signature]
class Collection(Collection) {
    @[reader]
    command get [string id] :: void -> Record | null
        refers python:mongo.Get;
    
    @[updater]
    command set [string? id] :: Record -> void
        refers python:mongo.Set;
    
    @[reader]
    command select :: QueryExpr -> [Record*]
        refers python:mongo.Select;
    
    @[updater]
    command delete [string? id] :: QueryExpr -> void
        refers python:mongo.Delete;
    
    @[reader]
    command exists [string id] :: void -> boolean {
        get %1 | when {
            None => false,
            * => true
        }
    };
    
    @[reader]
    command clone [string id] :: void -> void {
        [get %1, undefined] | xjson:put $._id | set
    };

    @[reader]
    command list-ids :: QueryExpr -> [string*] {
        select | each {
            $._id
        }
    };
};


type Database = foreign;
type Collection = foreign;

