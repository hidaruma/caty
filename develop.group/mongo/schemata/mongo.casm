/** 
 * mongodb操作用モジュール
 * JSONストレージバックエンドとは別個に作ってある。
 */
module mongo in casm;

/** 
 * mongodbに格納される形の値に変換する。
 */
command to-bson :: object -> BSON
    refers python:mongo.ToBSON;

type BSON = {
    *: (string | number | boolean | ObjectId | BSON | binary),
};

type ObjectId = foreign;

@[signature]
class MongoHandler(foreign) {
};

type MongoConfig = {
    "host": string,
    "port": integer,
};

facility MongoHandler(null) configured MongoConfig
    conforms MongoHandler
    refers python:mongolib.MongoHandler;

entity mongo = MongoHandler(null);
type Status = {
  "status": ("stopped" | "running" | "unknown"),
  "host": string(remark="ホストアドレス"),
  "port": integer(remark="ポート番号"),
};

command status :: void -> Status
    reads mongo
    refers python:mongo.Status
;

command list-databases :: void -> [string*]
    uses mongo
    refers python:mongo.ListDatabases
;

command create-database [string] :: void -> Database
    uses mongo
    refers python:mongo.CreateDatabase
;

command activate-database [string] :: void -> Database
    uses mongo
    refers python:mongo.ActivateDatabase
;

@[signature]
class Database(Database) {
    command list-collections :: void -> [string*]
        refers python:mongo.ListCollections;
    command drop-collection [string] :: void -> void
        refers python:mongo.DropCollection;
    command create-collection [string] :: void -> Collection
        refers python:mongo.CreateCollection;
    command clear-collection [string] :: void -> void
        refers python:mongo.ClearCollection;
};

@[signature]
class Collection(Collection) {
    command get [string id] :: void -> Record | null
        refers python:mongo.Get;
    
    command set [string? id] :: Record -> void
        refers python:mongo.Set;
    
    command select :: QueryExpr -> [Record*]
        refers python:mongo.Select;
    
    command delete [string? id] :: QueryExpr -> void
        refers python:mongo.Delete;
    
    command exists [string id] :: void -> boolean {
        get %1 | when {
            None => false,
            * => true
        }
    };
    
    command clone [string id] :: void -> void {
        [get %1, undefined] | xjson:put $._id | set
    };

    command list-ids :: QueryExpr -> [string*] {
        select | each {
            $._id
        }
    };

};


type Database = foreign;
type Collection = foreign;
type Record = {
    *:(BScalar | Record | BArray)?
};
type BScalar = string | number | boolean | EncodedObjectId;
type BArray = [(BScalar|BArray|Record)*];
type QueryExpr = Record | null;
type EncodedObjectId = @ObjectId string;

command f {*:any?} [string*] :: void -> void {
%_OPTS|dump --force;%_ARGV|dump --force
};

command g {*:any?} [string*] :: void -> void {
f %--* %#
};
