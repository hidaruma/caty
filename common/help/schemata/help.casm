// -*- coding: utf-8 -*-
/** ヘルプページの出力のためのユーティリティ
 * 
 */
module help; 

type CmdInfo = inspect:ShortProfile;

type CmdList = [CmdInfo*];

type ModInfo = {
 /** モジュールフルネーム */
 "fullname" : string(remark="アプリケーションで修飾されたモジュール名"),

 /** コマンド情報配列 */
 "cmds" : CmdList,
};

type ModInfoList = [ModInfo*];

/** CmdInfoに型変数配列を文字列化したものを追加
 * CmdInfoはオープン制約なので、
 * プロパティの追加は自由で、バリデーションにも影響しない。
 */
type CmdInfo2 = CmdInfo & {
 "typeVarsStr" : string,
 * : any?
};

command modify-cmd-info :: CmdInfo -> CmdInfo2 {
 pass > in | $.typeVars | text:join ", " |
 cond {
  "" => "",
   * => ["<", pass, ">"] | text:concat,
 } > vars;

 [%in, %vars] | xjson:put $.typeVarsStr
};

command mods [string appName] :: void -> ModInfoList {
 inspect:list-mod %1 | 
 each {
  pv name >n;
  [%1, ":", %n] | text:concat | dump > mod;
  {"fullname":%mod, "cmds":inspect:list-cmd %mod  | each {modify-cmd-info}}
 }
};

command user :: void -> ModInfoList {
 apps | each {pv name} > app-names;
 %app-names |
 each {
  pass > app;
  mods %app
 } | xlist:flatten
};

command core :: void -> ModInfoList { 
 mods caty
};

// End of Module
