// -*- coding: utf-8 -*- 
/** プロジェクトサマリー */
module prj-summary in cara;


/*
{
    "name": "reif.t",
    "literate": false,
    "syntax": "casm",
    "place": "schemata",
    "document": {
        "description": "undocumented"
    },
    "annotations": {}
}
*/

type Doc = reif:Doc;

type Summary = {
 "name": string,
 "appNames": [string*],
 "apps" : [App*]
};


type App = {
 "name": string,
 "group": (string|null),
 "document": Doc,
 "modNames": [string*],
 "mods": [Mod*],
};

type Mod = {
 "appName": string,
 "name": string,
 "deprecated": boolean?,
 "document": Doc,
};

command doc-to-html :: Doc -> Doc  {
 pass > in |
 [
   $.description      > desc,
   $.moreDescription? > more,
 ];
 %more? | 
 when {
   undefined => %in,
   string    => 
      do {
          %desc                >: "description",
          %more | text:creole  >: "moreDescription",
       }
   ,
 }
};

/*
 "deprecated": {
     "name": "deprecated",
     "value": null
 },

  $.annotations.deprecated?=false >: "deprecated",

*/

command deprecated-to-boolean :: object? -> boolean {
 when {
  undefined => false,
  object    => true,
 }
};

command gen :: void -> Summary {

 proj > prj-name;
 sreif:list-applications | list:sort --key=name > orig-apps  | each {$.name} > appNames;
 %orig-apps |
 each {
   [
     $.name     > appName,
     $.group?   > group,
     $.document > appDocument,
   ];
   [%appName, "::"] | text:concat > app;
   sreif:list-modules %app | 
   take {[$.name, "public"] | eq --boolean | not} | list:sort --key=name > orig-mods | each {$.name} > modNames;
   %orig-mods |
   each {
      do {
        %appName    >: "appName",
        $.name      >: "name",
        $.annotations.deprecated? | deprecated-to-boolean >: "deprecated",
        $.document | doc-to-html >: "document",
      }
   } > mods;
   do {
     %appName     >: "name",
     %group?      >: "group",
     %modNames    >: "modNames",
     %mods        >: "mods",
     %appDocument | doc-to-html >: "document",
   }
 } > apps;
 
 do {
   %prj-name >: "name",
   %appNames >: "appNames",
   %apps     >: "apps",
 }

};

/** 型情報 */
type Type = {
    "document": Doc,
    "name": string,
};

/** 型パラメータ */
type TypeParam = reif:TypeParam;
/** コマンド情報 */
type Command = {
 "name": string,
 "typeParams": [TypeParam*],
 "document": Doc,
 "profile": reif:Profile,
};

type ModInfo = {
 "deprecated": boolean,
 "name": string,
 "appName":string,
 "document": Doc,
 "typeNames": [string*],
 "types": [Type*],
 "commandNames": [string*],
 "commands": [Command*],
};

command modify-type-info :: reif:TypeSummary -> Type {
 do {
   $.name        >: "name",
   $.document  | doc-to-html >: "document",
 }
};


command modify-command-info :: reif:CommandSummary -> Command {
 do {
   $.name        >: "name",
   $.typeParams  >: "typeParams",
   $.document  | doc-to-html >: "document",
   $.profiles.0  >: "profile",
 }
};

/**/

command show-mod [string app, string mod] :: void -> ModInfo {
 [
   %1 > app,
   %2 > mod,
 ];
 [%app, "::", %mod, ":" ] | text:concat > modPath;

 sreif:show-module %modPath | 
 [ 
   $.annotations.deprecated?  | deprecated-to-boolean > deprecated,
   $.document   > document,
   $.place      > place,
 ];
 sreif:list-types %modPath |each {modify-type-info}  > types | each{$.name} > typeNames;
 sreif:list-commands %modPath > commands | each{$.name} > commandNames;
 
 do {
   %deprecated >: "deprecated",
   %mod      >: "name",
   %app      >: "appName",
   %document | doc-to-html >: "document",
   %typeNames>: "typeNames",
   %types    >: "types",
   %commandNames>: "commandNames",
   %commands | each {modify-command-info}  >: "commands",
 }

};

resource ShowMod ("/show-mod.html") {
 action get ("/GET")
 {
   "app": string,
   "mod": string,
 }
 ::
 {
    show-mod %app %mod | print %0
 };

};

type FileInfo = {
  "name": string,
  "appName": string,
  "content": string?,
};

/** プレース（のロール）名、アプリケーション名、パッケージ修飾モジュール名からフィルのパスを作る */
command make-file-path :: [string place, string app, string mod, boolean literate]  -> string {
 [
   nth 1 > place,
   nth 2 > app,
   nth 3 > mod,
   nth 4 > literate,
 ];
 %place | 
 cond {
   "schemata" => ".casm",
   "actions"  => ".cara",
 } > fileExt0;
 %literate |
 cond {
   true => [%fileExt0, ".lit"] | text:concat,
   false => %fileExt0,
 } > fileExt;

 %mod | text:split "." | text:join "/" > mod-path;

 [%place, "@", %app, ":/", %mod-path, %fileExt] | text:concat
};


command read-file [string app, string mod] :: void -> FileInfo {
 [
   %1 > app,
   %2 > mod,
 ];
 [%app, "::", %mod, ":" ] | text:concat > modPath;
 sreif:show-module %modPath | 
 [
    $.place    > place,
    $.literate > literate,
 ];
 [%place, %app, %mod, %literate] | make-file-path > filePath;
 file:read %filePath > content;

 do {
   %mod     >: "name",
   %app     >: "appName",
   %content >: "content",
 }

};


resource ReadFile ("/read-file.html") {
 action get ("/GET")
 {
   "app": string,
   "mod": string,
 }
 ::
 {
    read-file %app %mod | print %0
 };

};
