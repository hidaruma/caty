// -*- coding: utf-8 -*- 
module db;

/** コレクション参照型（実験的） 
 */
type reference<C> = @__r {
 "a": [integer],
 "t": typeName<C>, // targetName<C> が望ましいだろう。
};

/* 型パラメータを持つクラス、型に対して汎用なコマンドを定義 
 */
class WebMethod<Key, Rec> = {
    command GET [Key id] :: void -> any {
        %1 > id;
        try { get %id } | catch {
            normal => pass,
            except => //レコードがなかったら適当な値を生成
              gen:sample<Rec> --string=implied | insert --output-rec %id,
        }
    };
    
    command emit-normal :: any -> WebOutput {
      xj2h:xjson-to-html | xjx:markup | response --ext=html
    };
};

/* == コレクションの定義 == */


/**
 * ランチのコレクション
 *
 */
collection lunch of {
 "id": integer, // とりあえず、gen:sampleが空にしないように必須にしておく
 "dateTime": ex:datetime,
 "author": {
      "name": ex:personName,
      "link": reference<person>,
  },
 "next" : reference<lunch>?,
 "prev" : reference<lunch>?,
 "content": {
    "写真": [ex:photo, ex:photo*]?,
    "コメント" : ex:sentence?,
  },
};
class lunch &= WebMethod<integer, recordType<lunch> >;



/**
 * 会社のコレクション
 *
 */
collection company of {
 "id": integer, // とりあえず、gen:sampleが空にしないように必須にしておく
 "会社名": string,
 "代表": reference<person>,
};
class company &= WebMethod<integer, recordType<company> >;


/**
 * 人物のコレクション
 *
 */
collection person of {
 "id" : integer,  // とりあえず、gen:sampleが空にしないように必須にしておく
 "名前": {
     "名": ex:givenName,
     "性": ex:familyName,
  },
 "年齢": integer(minimum=20, maximum=30),
 "知り合い達": [reference<person>*], //(maxItems=5), // gen:sampleが見ない
 "会社":  reference<company>?,
};
class person &= WebMethod<integer, recordType<person> >;

