// -*- coding: utf-8 -*- 

module db;

/** レコードの自動生成機能をサポートするMixin */
class GenMix<Key=Key default integer, Rec> = {
 command generate [integer(minimum=0, maximum=500) n] :: void -> any {
   list:range 0 %1 |
   each {
      gen:sample<Key> > id;
      gen:sample<Rec> > rec;
      exists %id |
      case {
        true  => undefined,
        false => %rec | insert %id; %id
      }
   } | 
   list:tighten > inserted;
   {
     "inserted": %inserted,
     "count": (%inserted | list:length), 
   }
 };

};


/** コレクション参照型（実験的） 
 */
type reference<C> = @__r {
 "a": [integer],
 "t": typeName<C>, // targetName<C> が望ましいだろう。
};

/** モジュールのエミッター */
command emit-normal {"ft": string?} :: any -> WebOutput {
  pass > in;
  %ft?="json" | text:split "-" | =[pass > format, pass> pf];
  %format |
  cond {
    "json" => %in | xjson:to-json | json:response,

    "html" => %pf?="plain" | dump --prefix=pf |
              cond {
                 "plain" => %in | wrap-lib:to-plain-html %UUSERVER_URL | response --ext=html,
                 "jqm"   => %in | wrap-lib:to-jqm-html   %UUSERVER_URL | response --ext=html,
                  *      => "output format error" | json:response,
              }
              ,

    *      => "output format error" | json:response,
  }
};

class home = {
 command get [(null|number|string) id, string?] :: void -> any {
  {
    "title": "home",
    "content": "hello",
  }
 };
};

command home :: void -> Reference {
 make-ref db:home null
};

/* 
 * == コレクションの定義 == 
 */


/**
 * ランチのコレクション
 *
 */
collection lunch of {
 "id": integer, // とりあえず、gen:sampleが空にしないように必須にしておく
 "dateTime": ex:datetime,
 "author": {
      "name": ex:personName,
      "link": reference<person>,
  },
 "next" : reference<lunch>?,
 "prev" : reference<lunch>?,
 "content": {
    "写真": [ex:photo, ex:photo*]?,
    "コメント" : ex:sentence?,
  },
  "detail": {
    "お店の地図": ex:photo,
    "詳しい感想": ex:parag?,
    "似たランチ一覧": [reference<lunch>, reference<lunch>*]?,
  }
};
class lunch &= 
   web-util:MagicGetMix< recordType<lunch> > 
 & web-util:IndexMix
 & GenMix<Key=integer, recordType<lunch> > 
 ;

/**
 * 会社のコレクション
 *
 */
collection company of {
 "id": integer, // とりあえず、gen:sampleが空にしないように必須にしておく
 "会社名": string,
 "代表": reference<person>,
};
class company &= 
   web-util:MagicGetMix< recordType<company> >  
 & web-util:IndexMix
 & GenMix< recordType<company> > 
;

/**
 * 人物のコレクション
 *
 */
collection person of {
 "ID" : integer,  // とりあえず、gen:sampleが空にしないように必須にしておく
 "ハンドル": ex:something,
 "名前": {
     "名": ex:givenName,
     "性": ex:familyName,
  },
 "年齢": integer(minimum=20, maximum=30),
 "知り合い達": [reference<person>*], //(maxItems=5), // gen:sampleが見ない
 "会社":  reference<company>?,
 "ランチレポート": [reference<lunch>, reference<lunch>*]?,

} identified ID;
class person &= 
   web-util:MagicGetMix< recordType<person> > 
 & web-util:IndexMix
 & GenMix<K=integer, recordType<person> > 
;


/** fetchを使った事例
 */
class personAlt = {
 command GET [integer id, string? path] :: void -> any {
   %1 > id;

   make-ref db:person %id | dump --prefix="reference:" |
   fetch --deref-depth=2 {
     "ID": _,
     "知り合い達": [_*]?,
//     "知り合い達": _?,
   }
 };

};

/**
 * ランチのサマリー
 *
 */
class lunchSummary = {

 command GET [integer id, string? path] :: void -> any {

   make-ref db:lunch %1 %2? | dump --prefix="reference:" |
   fetch --deref-depth=1
   {
     "id": _,
     "author": _,
     "content": _,
     "detail": &?,
   }
 };

};
